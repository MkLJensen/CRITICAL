class RecyclingPlant

instance variables

rcTrucks : set of GarbageTruck := {};

sortedGarbage : GarbageSorter`GarbageMap := {   <GLASSID>   |-> {},
                                                <METALID>   |-> {},
                                                <PAPERID>   |-> {},
                                                <PLASTICID> |-> {}
                                            };


operations

public Step : () ==> ()
Step() == (
    if card rcTrucks > 0 then
    (
        dcl sortedMap : GarbageSorter`GarbageMap := sortAllTrucks();
            handleGarbageMap(sortedMap);
    )
);


private handleGarbageMap : GarbageSorter`GarbageMap ==> ()
handleGarbageMap(gpMap) ==
(
    IO`println("Sorted Garbage: ");
    for all gptype in set dom gpMap do
    (
        cases gptype:
            <GLASSID> -> IO`println("Glass: "),
            <METALID> -> IO`println("Metal: "),
            <PAPERID> -> IO`println("Paper: "),
            <PLASTICID> -> IO`println("Plastic: "),
            others -> skip
        end;

        -- Set can abstract the maps set as a Large GarbagePack, therefore these functions work
        IO`println("Weight");
        IO`println(GLOBAL`SumWeightGarbagePack(gpMap(gptype)));
        IO`println("Volume");
        IO`println(GLOBAL`SumDimensionGarbagePack(gpMap(gptype)));
        IO`println("");
        sortedGarbage(gptype) := {};
    );
);

public addFilledTrucksToPlant : set of GarbageTruck ==> ()
addFilledTrucksToPlant(filledTruck) == 
    rcTrucks := rcTrucks union filledTruck;

private sortAllTrucks : () ==> GarbageSorter`GarbageMap
sortAllTrucks() == (
    for all t in set rcTrucks
        do 
        (
            let x = getSetOfIndividualGarbageFromTruck(t) in
                sortedGarbage := GarbageSorter`MapCombine(sortedGarbage,GarbageSorter`sortSetofGarbagePack(x));
            t.emptyTruck();
            GarbageSortingSystem`garbageSortingController.addTrucks({t});
            rcTrucks := rcTrucks \ {t};
        );
    return sortedGarbage 
);


functions
private getSetOfIndividualGarbageFromTruck : GarbageTruck -> set of GarbageType
getSetOfIndividualGarbageFromTruck(gbTruck) == (
    dunion {i.getGarbagePack() | i in set gbTruck.getTruckGarbage()}
);


end RecyclingPlant