class Environment is subclass of GLOBAL

types

inline  = seq of char * set of GarbagePack * Time;   

instance variables 
FinishedCollecting : bool := false;

io : IO := new IO();

inlines     : seq of inline  := [];

operations

public Run : () ==> ()
Run() ==
    (
        while (not FinishedCollecting) do 
        (
        updateAddresses();
        GarbageSortingSystem`garbageSortingController.Step();
        GarbageSortingSystem`plant.Step();
        World`timer.StepTime();
        );
    );

private updateAddresses : () ==> ()
updateAddresses() ==
(
    if len inlines > 0
    then 
        (dcl curtime : Time := World`timer.GetTime(),
            doneRead : bool := false;
            while not doneRead do
            (
                def mk_(adrString, gbpackset, objTime) = hd inlines
                in
                    if objTime <= curtime
                    then (
                            GarbageSortingSystem`address_repository.addToAddresses({adrString |-> gbpackset});
                            inlines := tl inlines; 
                            doneRead := len inlines = 0;
                        )
                    else
                        doneRead := true
            )
        )
    else
        FinishedCollecting := true

);

public Environment : seq of char ==> Environment
Environment(fname) ==
     def mk_(-,input) = io.freadval[seq of inline](fname) 
        in
            inlines := input;

end Environment