class Environment is subclass of GLOBAL

types

inline = seq of char * seq of gbpackinputtype * Time

--[mk_("Brammersgade",[mk_gbpackinputtype(<METALID>,mk_dimensionsType(10,5,3),20)],0)]

instance variables 
FinishedCollecting : bool := false;

inlines     : seq of inline  := [];

operations

public Run : () ==> ()
Run() ==
    (
        while (not FinishedCollecting) do 
        (
        updateAddresses();
        GarbageSortingSystem`garbageSortingController.Step();
        GarbageSortingSystem`plant.Step();
        World`timer.StepTime();
        );
    );

private updateAddresses : () ==> ()
updateAddresses() ==
(
    if len inlines > 0
    then 
        (dcl curtime : Time := World`timer.GetTime(),
            doneRead : bool := false;
            while not doneRead do
            (
                def mk_(adrString, gbpackinput, objtime) = hd inlines
                in
                    if objtime <= curtime
                    then (
                        dcl gtset : set of GarbageType;
                            for gps in gbpackinput
                            do
                            (
                                cases gps.#1:
                                <GLASSID> -> gtset:= gtset union {new Glass(mk_dimensionsType(gps.#2.width,gps.#2.length,gps.#2.height), gps.#3)},
                                <METALID> -> gtset:= gtset union {new Metal(mk_dimensionsType(gps.#2.width,gps.#2.length,gps.#2.height), gps.#3)},
                                <PAPERID> -> gtset:= gtset union {new Paper(mk_dimensionsType(gps.#2.width,gps.#2.length,gps.#2.height), gps.#3)},
                                <PLASTICID> -> gtset:= gtset union {new Plastic(mk_dimensionsType(gps.#2.width,gps.#2.length,gps.#2.height), gps.#3)},
                                others -> skip
                                end;
                            );
                        GarbageSortingSystem`address_repository.addToAddresses({adrString |-> {new GarbagePack(gtset)}});
                        inlines := tl inlines; 
                        doneRead := len inlines = 0;
                        )
                    else
                        doneRead := true
            )
        )
    else
        FinishedCollecting := true

);

public Environment : seq of char ==> Environment
Environment(fname) ==
(
    def mk_(-,input) = IO`freadval[seq of inline](fname) 
        in
            inlines := input;
)
end Environment